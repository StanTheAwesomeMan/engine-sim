import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label rot                 (   0.125 * cycle     )
label bore                (   16.67 * units.mm  )
label cycle               (     720 * units.deg )
label stroke              (   17.00 * units.mm  )
label rod_mass            (      50 * units.g   )
label rev_limit           (12500.00 * units.rpm )
label rod_length          (     1.4 * stroke    )
label crank_mass          (     2.0 * units.kg  )
label flywheel_mass       (     4.0 * units.kg  )
label flywheel_radius     (     1.2 * units.inch)
label compression_height  (     4.0 * units.mm  )

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

/*
Intake runner Diameter: 0.52
Intake runner Length: 14.66 or 29.32
*/

private node Toyan_FS_V800_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 1 * units.cc;
    input intake_runner_volume: 24.11 * units.cc;
    input intake_runner_cross_section_area: 13.1237 * units.mm;
    input exhaust_runner_volume: 24.11 * units.cc;
    input exhaust_runner_cross_section_area: 13.1237 * units.mm;

    input flow_attenuation: 1.0;
    input lift_scale: 1.0;
    input flip_display: false;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 58 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 103 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 156 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 214 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 249 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 268 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 280 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 280 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 281 * flow_attenuation)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 37 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 72 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 113 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 196 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 222 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 245 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 246 * flow_attenuation)

    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: intake_runner_volume,
        intake_runner_cross_section_area: intake_runner_cross_section_area,
        exhaust_runner_volume: exhaust_runner_volume,
        exhaust_runner_cross_section_area: exhaust_runner_cross_section_area,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: standard_valvetrain(
            intake_camshaft: intake_camshaft,
            exhaust_camshaft: exhaust_camshaft
        ),
        flip_display: flip_display
    )
}

private node Toyan_FS_V800_camshaft {
    input lobe_profile;
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: lobe_profile;
    input lobe_separation: 114 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;  
    input advance: 0 * units.deg; 
    input base_radius: 0.1 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params (
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)

    label rot360(360 * units.deg)

    // 1 2 3 4 7 8 5 6
    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot) // 1
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot) // 3
        .add_lobe(rot360 - exhaust_lobe_center + 8 * rot) // 5
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot) // 7
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot) // 1
        .add_lobe(rot360 + intake_lobe_center + 2 * rot) // 3
        .add_lobe(rot360 + intake_lobe_center + 8 * rot) // 5
        .add_lobe(rot360 + intake_lobe_center + 4 * rot) // 7

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot) // 2
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot) // 4
        .add_lobe(rot360 - exhaust_lobe_center + 9 * rot) // 6
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot) // 8
    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot) // 2
        .add_lobe(rot360 + intake_lobe_center + 3 * rot) // 4
        .add_lobe(rot360 + intake_lobe_center + 9 * rot) // 6
        .add_lobe(rot360 + intake_lobe_center + 5 * rot) // 8
}

public node Toyan_FS_V800 {
    alias output __out: engine;

    engine engine(
        name: "Toyan V8 FS-V800",
        starter_torque: 100 * units.lb_ft,
        starter_speed: 200 * units.rpm,
        redline: rev_limit,
        throttle_gamma: 2.0,
        fuel: fuel(
            max_turbulence_effect: 10.0,
            max_dilution_effect: 20.0,
            burning_efficiency_randomness: 0.1,
            max_burning_efficiency: 1.0
        ),
        simulation_frequency: 10000
    )

    wires wires()

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 0.04 * units.kg, radius: 1.0 * units.cm)
    )

    label v_angle(90 * units.deg)
    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 0.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90 * units.deg + (v_angle / 2.0)
    )

    // 72 degrees
    rod_journal rj0(angle: 0 * v_angle)
    rod_journal rj1(angle: 2 * v_angle)
    rod_journal rj2(angle: 3 * v_angle)
    rod_journal rj3(angle: 1 * v_angle)
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: (100) * units.g, // 414 - piston mass, 152 - pin weight
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    intake intake(
        plenum_volume: 3.11 * units.cc,
        plenum_cross_section_area: 0.2 * units.cm2,
        intake_flow_rate: k_carb(10.0),
        runner_flow_rate: k_carb(2.0),
        runner_length: 17 * units.mm,
        idle_flow_rate: k_carb(0.0),
        idle_throttle_plate_position: 0.998,
        velocity_decay: 0.5
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(20.0),
        primary_tube_length: 17 * units.mm,
        primary_flow_rate: k_carb(10.0),
        velocity_decay: 1.0
    )

    impulse_response ir0(filename: "../../sound-library/smooth/smooth_32.wav", volume: 0.01)

    exhaust_system exhaust0(
        es_params,
        audio_volume: 1.0,
        length: 1 * units.inch,
        impulse_response: ir0
    )
    exhaust_system exhaust1(
        es_params,
        audio_volume: 1.0,
        length: 1 * units.inch,
        impulse_response: ir0
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + 1 * units.mm
    )

    cylinder_bank b0(bank_params, angle: v_angle / 2.0)
    cylinder_bank b1(bank_params, angle: -v_angle / 2.0)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire1,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire3,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire5,
            sound_attenuation: 1.1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire7,
            sound_attenuation: 0.9
        )
        .set_cylinder_head(
            Toyan_FS_V800_head(
                intake_camshaft: camshaft.intake_cam_0,
                exhaust_camshaft: camshaft.exhaust_cam_0,
                flip_display: true,
                flow_attenuation: 1.5)
        )
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire2,
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire4,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.0)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire8,
            sound_attenuation: 1.1
        )
        .set_cylinder_head(
            Toyan_FS_V800_head(
                intake_camshaft: camshaft.intake_cam_1,
                exhaust_camshaft: camshaft.exhaust_cam_1,
                flow_attenuation: 1.5)
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    harmonic_cam_lobe intake_lobe(
        duration_at_50_thou: 230 * units.deg,
        gamma: 1.1,
        lift: 2.95 * units.mm,
        steps: 100
    )

    harmonic_cam_lobe exhaust_lobe(
        duration_at_50_thou: 230 * units.deg,
        gamma: 1.1,
        lift: 2.95 * units.mm,
        steps: 100
    )

    Toyan_FS_V800_camshaft camshaft(
        lobe_profile: "N/A",

        intake_lobe_profile: intake_lobe,
        exhaust_lobe_profile: exhaust_lobe,
        intake_lobe_center: 90 * units.deg,
        exhaust_lobe_center: 112 * units.deg,
        base_radius: 0.9 * units.inch
    )

    function timing_curve(4000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 12 * units.deg)
        .add_sample(4000 * units.rpm, 40 * units.deg)
        .add_sample(8000 * units.rpm, 40 * units.deg)
        .add_sample(12000 * units.rpm, 40 * units.deg)
        .add_sample(14000 * units.rpm, 40 * units.deg)
        .add_sample(18000 * units.rpm, 40 * units.deg)

    ignition_module ignition_module(
        timing_curve: timing_curve,
        rev_limit: rev_limit * units.rpm,
        limiter_duration: 0.1)
    ignition_module
            .connect_wire(wires.wire1,  0 * rot)
            .connect_wire(wires.wire2,  1 * rot)
            .connect_wire(wires.wire3,  2 * rot)
            .connect_wire(wires.wire4,  3 * rot)
            .connect_wire(wires.wire7,  4 * rot)
            .connect_wire(wires.wire8,  5 * rot)
            .connect_wire(wires.wire5,  6 * rot)
            .connect_wire(wires.wire6,  7 * rot)

    engine.add_ignition_module(ignition_module)
}

public node main {
    set_engine(Toyan_FS_V800())
}